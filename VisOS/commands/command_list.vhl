uint funcList;
uint funcListSize;

#include "../VisOS.Driver/cout.vhl"
#include "../utils/memory.vhl"
#include "../mmgr.vhl"

string(txt_init, "Initializing Command System..");
string(DEBUG_TAG, "[CMD] ");
string(txt_invoke, "Running Command: ");
string(txt_found, "Found Command: ");
string(txt_not_found, "Could not find Command.");

public void CMD_InitCommandList()
{
	funcListSize = 512;
	COUT_WriteString(&DEBUG_TAG, size_of(DEBUG_TAG));
	COUT_WriteLine( &txt_init, size_of(txt_init));
	if(funcList)
	{
		MMGR_Free(funcList);
	}
	funcList = MMGR_Alloc(funcListSize);
	MEM_Set( funcList, funcListSize, 0);
	return;
}

public void CMD_UnloadCommandList()
{
	MMGR_Free(funcList);
	funcList = 0;
	return;
}

private uint FindCommand(uint cmdPtr)
{
	
	uint current = 0;
	uint len = funcListSize;
	while(current < len)
	{
		uint checkPtr = funcList[current];
		if(checkPtr)
		{
			uint found = checkPtr(cmdPtr);

			if(found)
			{
				return current+1;
			}
		}
		current += 2;
	}
	COUT_WriteString(&DEBUG_TAG, size_of(DEBUG_TAG));
	COUT_WriteLine( &txt_not_found, size_of(txt_not_found));
	return 0;
}

public void CMD_InsertCommand(uint funcPtr, uint checkPtr)
{
	uint current = 0;
	while(current < funcListSize)
	{
		uint entry = funcList[current];
		if(!entry)
		{
			funcList[current] = checkPtr;
			uint funcIndex = current+1;
			funcList[funcIndex] = funcPtr;
			return;
		}
		current += 2;
	}
	return;
}

public void CMD_InvokeCommand(uint cmdPtr, uint cmdLen)
{
	uint cmdIndex = FindCommand(cmdPtr);
	if(cmdIndex)
	{
		uint ptr = funcList[cmdIndex];

		COUT_WriteString(&DEBUG_TAG, size_of(DEBUG_TAG));
		COUT_WriteString( &txt_invoke, size_of(txt_invoke));
		COUT_WriteNumber(ptr);
		COUT_WriteNewLine();

		uint argStart = MEM_FirstIndex(cmdPtr, 255, 0x20);
		uint sta = argStart+1;

		ptr(cmdPtr+sta, cmdLen - sta);
	}
	return;
}